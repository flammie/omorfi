Alphabet

! Basic finnish alphabet
a b c d e f g h i j k l m n o p q r s š t u v w x y z ž å ä ö
A B C D E F G H I J K L M N O P Q R S Š T U V W X Y Z Ž Å Ä Ö
‐ – %- 
’ ' %" ” »

! Morphophonemics
%{aä%}:a %{aä%}:ä %{oö%}:o %{oö%}:ö %{uy%}:u %{uy%}:y
%{aäe%}:a %{aäe%}:ä %{aäe%}:e
%{k%~%~%}:k %{k%~%~%}:0 %{k%~%~%}:' %{k%~%~%}:g %{k%~%~%}:j %{k%~%~%}:v
%{p%~%~%}:p %{p%~%~%}:0 %{p%~%~%}:v %{p%~%~%}:m 
%{t%~%~%}:t %{t%~%~%}:0 %{t%~%~%}:d %{t%~%~%}:r %{t%~%~%}:n %{t%~%~%}:l
%{g%~%~%}:g %{g%~%~%}:0 %{b%~%~%}:b %{b%~%~%}:0 %{d%~%~%}:d %{d%~%~%}:0
%{nlrs%}:n %{nlrs%}:l %{nlrs%}:r %{nlrs%}:s
%{aeiouyäö%}:a %{aeiouyäö%}:e %{aeiouyäö%}:i %{aeiouyäö%}:o
%{aeiouyäö%}:u %{aeiouyäö%}:y %{aeiouyäö%}:ä %{aeiouyäö%}:ö
%{aeiouyäö%}:0
n:0
e:i
t:0

! Absolute boundary (retained for further processing)
%{##%} %{#%} %{%_%} %{%-%}

! Grade markers
%{A%}:0 %{B%}:0 %{C%}:0 %{D%}:0 %{E%}:0 %{F%}:0 %{G%}:0 %{H%}:0 %{I%}:0
%{J%}:0 %{K%}:0 %{L%}:0 %{M%}:0 %{N%}:0 %{O%}:0 %{P%}:0 %{S%}:0 %{T%}:0

! Block vowel lengthening flag
%{BVL%}:0

! Deleted phonemes
%{%^a%}:0 %{%^e%}:0 %{%^i%}:0 %{%^o%}:0 %{%^u%}:0 %{%^y%}:0 %{%^ä%}:0 %{%^ö%}:0
%{%^front%}:0 %{%^back%}:0
;

Diacritics

! Diacritics and triggers
%{present%} %{comparative%} %{possessive%}
%{PLT%+%} %{PLT%?%} %{SGT%+%} %{SGT%?%}
%{←e%~i%} %{←n%~%} %{←i%~%}
%{AV%+%} %{AV%-%}
%{←t%~%}


;

Sets

Vowels = a e i o u y å ä ö á à â é è ê î ô û í ï ó ò ú ù A E I O U Y Ä Ö È ;
Consonants = b c d f g h j k l m n p q r s š t v w x z ž B C D F G H J K L M N P T Q R S Š T V W X Z Ž ;
Harmony = A O U a o u Ä Ö Y ä ö y %{%^a%} %{%^o%} %{%^u%} %{%^ä%} %{%^ö%} %{%^y%} %{%^front%} %{%^back%} %{#%} %{##%} ;
BackHarmony = A O U a o u %{%^a%} %{%^o%} %{%^u%} %{%^back%} ;
FrontHarmony = Ä Ö Y ä ö y %{%^ä%} %{%^ö%} %{%^y%} %{%^front%} ;
Lengthening = A E I O U Y Ä Ö a e i o u y ä ö %{%^a%} %{%^e%} %{%^i%} %{%^o%} %{%^u%} %{%^y%} %{%^ä%} %{%^ö%} %{BVL%} %{#%} %{##%} ;

BasicVowels = a e i o u y ä ö ;
GhostVowels = %{%^a%} %{%^e%} %{%^i%} %{%^o%} %{%^u%} %{%^y%} %{%^ä%} %{%^ö%} ;

FrontGhostVowels    = %{%^y%} %{%^ä%} %{%^ö%} ;
FrontVowels         =   y   ä   ö ;
FrontVowelHarmonyMP = %{uy%} %{aä%} %{oö%} ;

NonFrontVowelHarmonyMP    = %{aä%}   e   i %{oö%} %{uy%} ;
NonFrontGhostVowels       = %{%^a%} %{%^e%} %{%^i%} %{%^o%} %{%^u%} ;
NonFrontVowels            =   a   e   i   o   u ; 
NonFrontAccentAcuteVowels =   á   é   í   ó   ú ;
NonFrontAccentGraveVowels =   à   è   ï   ò   ù ;

WordChars = a b c d e f g h i j k l m n o p q r s š t u v w x y z ž å ä ö 
	A B C D E F G H I J K L M N O P Q R S Š T U V W X Y Z Ž Å Ä Ö 
	á à â é è ê î ô û È 
	%1 %2 %3 %4 %5 %6 %7 %8 %9 %0
	á à â ã ā ă ą ç ć ĉ ċ č đ ð ď é è ê ë ē ĕ ę ė ě ƒ ĝ ğ ġ ģ ȟ ħ í ì î ï ĩ ī ĭ 
	į ı ĳ ĵ ķ ĸ ĺ ļ ľ ŀ ł ñ ń ņ ň ŉ ŋ ó ò ô ō ŏ ŕ ŗ ř ś ŝ ş ſ ţ ť ŧ ß þ ú ù û ü
	ũ ū ŭ ů ų ŵ ý ŷ ÿ ű ź ż ʒ æ ø œ ő ə
	Á À Â Ã Ā Ă Ą Ç Ć Ĉ Ċ Č Ð Ď É È Ê Ë Ē Ĕ Ę Ė Ě Ĝ Ğ Ġ Ģ Ȟ Ħ Í Ì Î Ï Ĩ Ī Ĭ Į İ
	Ĳ Ĵ Ķ Ĺ Ļ Ľ Ŀ Ł Ñ Ń Ņ Ň Ŋ Ó Ò Ô Ō Ŏ Ŕ Ŗ Ř Ś Ŝ Ş Ţ Ť Ŧ Þ Ú Ù Û Ü Ũ Ū Ŭ Ů Ų Ŵ
	Ý Ŷ Ű Ź Ż Ʒ Æ Ø Œ Ő Ə
	‐ %: %- ’ %' %  %{#%} %{##%} ; 

GradeMarks = %{AV%+%} %{AV%-%} ;
Grades = %{A%} %{B%} %{C%} %{D%} %{E%} %{F%} %{G%} %{H%} %{I%} %{J%} %{K%} %{L%}
		 %{M%} %{N%} %{O%} %{P%} %{T%} ;
Gradations = GradeMarks Grades %{#%} %{##%} ;

Definitions

WordBoundary = [ %{##%}: | %{#%}: | %{%-%}: | %{%_%}: | %-: | :%- | % ] ;
NoHarmony = [ ? - [ Harmony: | WordBoundary ] ]* ;
NoLengthening = [ ? - [ BasicVowels: | GhostVowels: | %{aeiouyäö%}: | %{BVL%}: | :BasicVowels | %{##%}: ] ]* ;
NoGradeMarkers = [ ? - [ Grades: | WordBoundary ] ]* ;

Rules

"Possessive n deletion"
n:0 <=> _ %{←n%~%}: ;

"ks clitic t deletion"
t:0 <=> _ %{←t%~%}: ;


"Vowel Harmony"
Vh:Vv <=> BackHarmony: NoHarmony _ ; where Vh in ( %{aäe%} %{aä%} %{oö%} %{uy%} )
                                               Vv  in ( a a o u ) matched ;

"Comparative a:e only in bisyllabic stems"
%{aäe%}:e => WordBoundary [:Consonants* :Vowels+ :Consonants+] :Vowels* _ ;

"Block Vowel Lengthening"
%{aeiouyäö%}:0 <=> %{BVL%}: NoLengthening _ ;

"Vowel Lengthening Front"
%{aeiouyäö%}:Vf <=> [ Vf | Vfg: | :Vf ] NoLengthening _ ; where Vf in FrontVowels
													Vfg in FrontGhostVowels  matched ;
!													Vfh in FrontVowelHarmonyMP matched ;

"Vowel Lengthening Non-front"
%{aeiouyäö%}:Vnf <=> [ Vnf | Vng: | :Vnf ] NoLengthening _ ; 
									where Vnf in NonFrontVowels
										  Vng  in NonFrontGhostVowels matched ;
!										  Vnh  in NonFrontVowelHarmonyMP matched ; 

"N-assimilation"
%{nlrs%}:Ns <=> Ns :0* _ ; where Ns in ( l r s ) ;

"Derivational ja e:i change"
e:i <=> _ :0* %{←e%~i%}: ;

"Derivational i collapsing"
i:0 <=> _ :0* %{←i%~%}: ;





! Gradation
"Strong grade context limitations"
Cs:Cw /<= _ \[ %{%AV%+%}: | WordBoundary ]* %{AV%-%}: ; where Cs in
	(  %{k%~%~%}  %{p%~%~%}  %{t%~%~%}
	   %{d%~%~%}  %{g%~%~%}  %{b%~%~%} )
	Cw in ( k p t d g b ) matched ;

"Weak grade context limitations"
Cs:Cw /<= _ \[ %{%AV%-%}: | WordBoundary ]* %{AV%+%}: ; where Cs in
	(  %{k%~%~%}  %{p%~%~%}  %{t%~%~%}
	              %{p%~%~%} %{t%~%~%}
	   %{k%~%~%} %{p%~%~%} %{t%~%~%}
	   %{t%~%~%} %{t%~%~%} %{k%~%~%}
	   %{k%~%~%} %{d%~%~%} %{g%~%~%}
	   %{b%~%~%} %{t%~%~%} %{k%~%~%} )
	Cw in 
	( 0 0 0 
	    v d
	  g m l
	  n r j
	  v 0 0
	  0 0 ' ) matched ;

"Weakening gradation triggers"
Cs:Cw => _ NoGradeMarkers Cm: ; where Cs in
	(  %{k%~%~%}  %{p%~%~%}  %{t%~%~%}
	              %{p%~%~%} %{t%~%~%}
	   %{k%~%~%} %{p%~%~%} %{t%~%~%}
	   %{t%~%~%} %{t%~%~%} %{k%~%~%}
	   %{k%~%~%} %{d%~%~%} %{g%~%~%}
	   %{b%~%~%} %{t%~%~%} )
	Cw in 
	( 0 0 0 
	    v d
	  g m l
	  n r j
	  v 0 0
	  0 0 ) 
	Cm in ( %{A%} %{B%} %{C%} 
	      %{E%} %{F%}
	%{G%} %{H%} %{I%}
	%{J%} %{K%} %{L%}
	%{M%} %{N%} %{O%}
	%{P%} %{T%} ) matched ;

"K:0 gradation contexts"
%{k%~%~%}:0 | %{k%~%~%}:' <= _ \[ Gradations:0 | WordBoundary ]* %{D%}:0 \[ %{AV%+%}: | WordBoundary ]* [ %{AV%-%}: \[ WordBoundary ]* %{AV%+%}: ]* [ WordBoundary ] ;

"K:0 gradation requirements"
%{k%~%~%}:0 => _ \[ Gradations:0 | WordBoundary]* %{D%}:0 ;

"K:' gradation requirements"
%{k%~%~%}:' => _ \[ Gradations:0 | WordBoundary]* %{D%}:0 ;

"K:0 disallowed contexts"
%{k%~%~%}:0 /<= Vowels :Vx :0* _ :0* :Vx ;
	:Vx :0* _ :0* :Vx Vowels ; where Vx in BasicVowels matched ;

! disallow theoretically corrects like laki : la'it, piki : pi'et, 
! haku : ha'uista
"K:' disallowed contexts"
%{k%~%~%}:' /<= :Vx :0* _ :0* :Vy ; where Vx in BasicVowels Vy in BasicVowels mixed;

! vim:set ft=xfst-twol:
